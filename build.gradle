import groovy.json.JsonSlurper
import groovy.json.JsonOutput

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

apply plugin: 'net.minecraftforge.gradle'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'

version = "${projecte_version}"
group= "java.moze_intel" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "projecte"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: '20190214-1.13.1'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    accessTransformer = file('src/main/resources/META-INF/pe_at.cfg')

    // default run configurations.
    // these can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'

            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            mods {
                projecte {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')

            // recommended logging data for a userdev environment
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'

            // recommended logging level for the console
            properties 'forge.logging.console.level': 'debug'
            mods {
                projecte {
                    source sourceSets.main
                }
            }
        }
    }
}

/*repositories {
	maven { //MinecraftForge
		name "MinecraftForge Maven"
		url "http://files.minecraftforge.net/maven/"
	}

    maven {
        name 'Chisel'
        url "http://maven.tterrag.com/"
    }

    maven {
        name = "thiakil"
        url = "http://maven.thiakil.com"
    }

    maven {
        name 'JEI'
        url "http://dvs1.progwml6.com/files/maven"
    }

    maven {
        name 'jared'
        url "http://maven.blamejared.com"
    }
}*/
 
dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft 'net.minecraftforge:forge:1.13.2-25.0.64'

    testCompile group: 'junit', name: 'junit', version:'4.11'

    //deobfCompile "mezz.jei:jei_1.12:4.7.0.67"
    //deobfCompile "com.azanor.baubles:Baubles:1.12-1.5.2"
    //deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-${crafttweaker_version}"
    //deobfCompile "CraftTweaker2:CraftTweaker2-API:1.12-${crafttweaker_version}"
    //deobfCompile "CraftTweaker2:ZenScript:1.12-${crafttweaker_version}"

    compile group: "org.apache.commons", name: "commons-math3", version: "3.6.1"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft_version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'META_INF/mods.toml'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion': project.minecraft_version
    }
        
    // copy everything else, thats not the mods.toml
    from(sourceSets.main.resources.srcDirs) {
        exclude 'META_INF/mods.toml'
    }
}

jar {
    manifest {
        attributes([
            "Specification-Title": project.name,
            "Specification-Vendor": project.group,
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" : project.group,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

shadowJar {
    // Only shadow apache commons-math3
    dependencies {
        include dependency('org.apache.commons:commons-math3:.*')
    }
    exclude('org/apache/commons/math3/analysis/**')
    exclude('org/apache/commons/math3/complex/**')
    exclude('org/apache/commons/math3/dfp/**')
    exclude('org/apache/commons/math3/distribution/**')
    exclude('org/apache/commons/math3/filter/**')
    exclude('org/apache/commons/math3/fitting/**')
    exclude('org/apache/commons/math3/genetics/**')
    exclude('org/apache/commons/math3/geometry/**')
    exclude('org/apache/commons/math3/linear/**')
    exclude('org/apache/commons/math3/ml/**')
    exclude('org/apache/commons/math3/ode/**')
    exclude('org/apache/commons/math3/optim/**')
    exclude('org/apache/commons/math3/optimization/**')
    exclude('org/apache/commons/math3/primes/**')
    exclude('org/apache/commons/math3/random/**')
    exclude('org/apache/commons/math3/special/**')
    exclude('org/apache/commons/math3/stat/**')
    exclude('org/apache/commons/math3/transform/**')

    exclude('assets/org/**')
    exclude('META-INF/maven/**')

    // Relocate apache commons-math3 to prevent conflicts with other mods that include it
    relocate 'org.apache.commons.math3', 'moze_intel.projecte.shaded.org.apache.commons.math3'

    classifier = 'universal' // Replace the default JAR
}

reobf {
    shadowJar {
        // Reobfuscate the shadowed JAR
    }
}

// Dev jar (deobfuscated)
task devJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
    version = "${project.version}"

    println(defaultTasks)
}

task updateJSON {
	doLast {
    	def updateJsonFile = file('update.json')
    	def updateJson = new JsonSlurper().parse(updateJsonFile) as Map

    	updateJson."${minecraft_version}"."${project.version}" = "See https://minecraft.curseforge.com/projects/projecte/files for detailed information."

    	// Update promos
    	updateJson.promos."${minecraft_version}-latest" = "${project.version}"
    	updateJson.promos."${minecraft_version}-recommended" = "${project.version}"

    	updateJsonFile.write(JsonOutput.prettyPrint(JsonOutput.toJson(updateJson)))
	}
}

artifacts {
    archives devJar
}
